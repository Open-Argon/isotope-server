import "server" as *
import "randstr" as *
import "database" as *

let push(req, res) = do
    let name = req.formdata.get("name", null)
    if (name == null) do
        res.code = 400
        res.send("Missing name")
        return
    let version = req.formdata.get("version", null)
    if (version == null) do
        res.code = 400
        res.send("Missing version")
        return
    let file = req.files.file
    let latest = req.formdata.latest
    let package = packages.get(name)
    if (package == null) do
        res.code = 404
        return res.send("Package not found")
    let acc = account.getByToken(req.headers.get("isotope-auth", ""))
    if (acc == null) do
        res.code = 403
        return res.send("You are not logged in")
    if (acc.id != package.owner) do
        res.code = 403
        return res.send("You are not the owner of this package")
    if ("order" not in package) package.order = {}
    if (package.versions.keys().length == 0) latest = "true"
    let currentVersion = package.versions.get(version)
    let directory = path.join([program.cwd, "packages", name])
    if (currentVersion == null) do
        currentVersion = {name:name, version:version}
        package.versions[version] = currentVersion
    let filename = version+".tar.gz"
    let p = path.join([directory, filename])
    term.log(directory)
    path.mkAllDir(directory)
    file.save(p)
    currentVersion.filename = filename
    package.order[version] = time.now().unix()
    if (latest == "true") do
        package.versions['latest'] = {redirect: version}
    makeChange()
    res.send("OK")
app.route("/isotope-push", ['POST'], push)