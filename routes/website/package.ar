import "../server" as *
import "../save" as *
import "url.ar" as url
import "bleach.ar" as bleach
import "shlex.ar" as shlex

let package(req, res) = do
    let name = req.query.get("name", null)
    let version = req.query.get("version", "latest")
    let commandName = name
    if (version != "latest") commandName = commandName + "@"+version
    let command = shlex.join(["isotope", "install", commandName])
    let package = packages.get(name)
    if (package == null) do
        res.code = 404
        res.renderTemplate("404.html", {})
        return
    if ("order" not in package) do
        package.order = {}
        changes.n = changes.n + 1
    if (version not in package.versions) do
        res.code = 404
        res.renderTemplate("404.html", {})
        return
    let versions = []
    let keys = package.versions.keys()
    let sortFunc(v) = do
        if (v == "latest") return infinity
        return package.order.get(v, 0)
    keys.sort(true, sortFunc)
    for (i from 0 to keys.length) do
        let versioni = keys[i]
        if (version == versioni) continue
        let URL = "/package?"+url.queryStr({name:name, version:versioni})
        versions.append("<a href='"+URL+"'>"+bleach.clean(name+"@"+versioni)+"</a>")
    res.renderTemplate("package.html", {name:name, version:version, versions:versions.join(""), command:command, commandComma:command.replace("'", "\\'")})
app.route("/package", null, package)