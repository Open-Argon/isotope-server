import "../server" as *
import "../save" as *
import "url.ar" as url
import "bleach.ar" as bleach
import "shlex.ar" as shlex

let package(req, res) = do
    let name = req.query.get("name", null)
    let version = req.query.get("version", "latest")
    let commandName = name
    if (version != "latest") commandName = commandName + "@"+version
    let command = shlex.join(["isotope", "install", commandName])
    let package = packages.get(name)
    if (package == null) do
        res.code = 404
        res.renderTemplate("404.html", {})
        return
    if ("order" not in package) do
        package.order = {}
        changes.n = changes.n + 1
    if (version not in package.versions) do
        res.code = 404
        res.renderTemplate("404.html", {})
        return
    let versions = []
    let keys = package.versions.keys()
    keys.sort(true, (v) = package.order.get(v, 0))
    for (i from 0 to keys.length) do
        let versioni = keys[i]
        let URL = "/package?"+url.queryStr({name:name, version:versioni})
        let html = null
        if (versioni == version) html = "<a href='"+URL+"' class='selected'>"+bleach.clean(name+"@"+versioni)+"</a>"
        else html = "<a href='"+URL+"'>"+bleach.clean(name+"@"+versioni)+"</a>"
        if (versioni == "latest") versions.insert(0, html)
        else versions.append(html)
    res.renderTemplate("package.html", {name:name, version:version, versions:versions.join(""), command:command, commandComma:command.replace("'", "\\'")})
app.route("/package", null, package)